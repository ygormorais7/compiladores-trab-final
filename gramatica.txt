<program>           ::= "Program" <id> "{" <decl_list> <stmt_list> "}"

<decl_list>         ::= <decl> <decl_list> | ε
<decl>              ::= "const" <id> "=" <literal> ";" 
                        | <type> <id_list> ";"

<type>              ::= "int"
                        | "float"
                        | "bool"
                        | "str"

<id_list>           ::= <id> <id_tail>
<id_tail>           ::= "," <id> <id_tail> | ε

<stmt_list>         ::= <stmt> <stmt_list> | ε
<stmt>              ::= <assign_stmt>
                        | <print_stmt>
                        | <input_stmt>
                        | <if_stmt>
                        | <while_stmt>

<assign_stmt>       ::= <id> "=" <exp> ";"
<print_stmt>        ::= "print" "(" <exp_list> ")" ";"
<input_stmt>        ::= "input" "(" <id_list> ")" ";"

<if_stmt>           ::= "if" "(" <exp> ")" "{" <stmt_list> "}" <else_part>
<else_part>         ::= "else" "{" <stmt_list> "}" | ε

<while_stmt>        ::= "while" "(" <exp> ")" "{" <stmt_list> "}"

<exp_list>          ::= <exp> <exp_tail>
<exp_tail>          ::= "," <exp_list> | ε

<exp>               ::= <exp_relational>

<exp_relational>    ::= <exp_arithmetic>
                        | <exp_arithmetic> (<rel_op>) (<exp_arithmetic>)

<exp_arithmetic>    ::= <exp_arithmetic> "+" <term>
                        | <exp_arithmetic> "-" <term>
                        | <term>

<term>              ::= <term> "*" <factor>
                        | <term> "/" <factor>
                        | <factor>

<factor>            ::= <id>
                        | int_const
                        | float_const
                        | string_const
                        | bool_const
                        | "(" <exp> ")"

<literal>           ::= int_const
                        | float_const
                        | string_const
                        | bool_const

<bool_const>        ::= true
                        | false

<rel_op>            ::=  '==' 
                        | '!=' 
                        | '>=' 
                        | '<=' 
                        | '>' 
                        | '<'